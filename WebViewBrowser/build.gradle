buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.9.+'
    }
}

apply plugin: 'android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.3'

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
    }
    buildTypes {
        release {
            runProguard false
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }
    productFlavors {
        staticbuild {
            proguardFile 'proguard-rules.txt'
            packageName "co.uk.gauntface.android.webviewbrowser"
        }

        liveserver {
            proguardFile 'proguard-rules.txt'
            packageName "co.uk.gauntface.android.webviewbrowser.liveserver"
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:19.0.+'
    compile project(':GauntFaceHelperLib')
}

task copyWebApplication(type: Copy) {
    from '../BrowserPages/dist'
    into 'src/main/assets/www'
}

task deleteWebApplication(type: Delete) {
    delete 'src/main/assets/www'
}

copyWebApplication.dependsOn ':BrowserPages:buildWebApp'
copyWebApplication.dependsOn deleteWebApplication

android.applicationVariants.all { variant ->
    if(variant.productFlavors[0].name != "liveserver") {
        //tasks.getByPath(":${project.name}:assemble${variant.name.capitalize()}").dependsOn copyWebApplication
    }
}
